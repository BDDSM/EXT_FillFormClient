
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	ПриСозданииНаСервереИзменитьПодключаемыеКоманды(Форма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ПриСозданииНаСервереИзменитьПодключаемыеКоманды(Форма, Отказ, СтандартнаяОбработка)
	
	Попытка // вместо перебора и проверок из Форма.ПолучитьРеквизиты()
		Команды = ПолучитьИзВременногоХранилища(Форма.ПараметрыПодключаемыхКоманд.АдресТаблицыКоманд);
	Исключение
		Возврат;
	КонецПопытки;
	
	Если Не ТипЗнч(Команды) = Тип("ТаблицаЗначений") Тогда
		Возврат;
	КонецЕсли;
	
	ВидОбработкиЗаполнениеОбъектов = "ЗаполнениеОбъектов"; // Хмм... ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиЗаполнениеОбъекта();
	СпособВызоваЗаполнениеФормы = Перечисления.СпособыВызоваДополнительныхОбработок.ЗаполнениеФормы;
	СпособВызоваВызовКлиентскогоМетода = Перечисления.СпособыВызоваДополнительныхОбработок.ВызовКлиентскогоМетода;
	
	Для Каждого Команда Из Команды Цикл
		
		Если Не Команда.Вид = ВидОбработкиЗаполнениеОбъектов Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не Команда.Обработчик = "ДополнительныеОтчетыИОбработки.ОбработчикКомандыЗаполнения" Тогда
			Продолжить;
		КонецЕсли;
		
		ДополнительныеПараметры = Команда.ДополнительныеПараметры;
		
		Если ДополнительныеПараметры.ВариантЗапуска = СпособВызоваЗаполнениеФормы
			И (СтрЗаканчиваетсяНа(ДополнительныеПараметры.Идентификатор, "Клиент")
				Или СтрЗаканчиваетсяНа(ДополнительныеПараметры.Идентификатор, "НаКлиенте")) Тогда
			
			ДополнительныеПараметры.ВариантЗапуска = СпособВызоваВызовКлиентскогоМетода;
			Команда.Обработчик = "ДополнительныеОтчетыИОбработкиКлиент.ОбработчикКомандыЗаполнения";
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
